#!/usr/bin/perl

use warnings;
use strict;

use Midnight::Game;

use IO::Prompt;
use Lingua::EN::Inflect qw(PL PL_V);

require Text::Wrap;
sub say (@) {
    local $Text::Wrap::columns;
    $Text::Wrap::columns = 80;

    print Text::Wrap::wrap('', '', @_), "\n";
}

my %command_defs = (
    commands    => { call => \&commands },

    quit        => { call => \&quit },
    exit        => { call => \&quit },

    look        => { look => 1 },
    examine     => { examine => 1 },

    forward     => { call => \&forward,
                     look => 1 },

    left        => { call => sub { turn("left") },
                     look => 1 },
    right       => { call => sub { turn("right") },
                     look => 1 },

    north       => { call => sub { face(Midnight::Map::Direction::NORTH) },
                     look => 1 },
    northeast   => { call => sub { face(Midnight::Map::Direction::NORTHEAST) },
                     look => 1 },
    east        => { call => sub { face(Midnight::Map::Direction::EAST) },
                     look => 1 },
    southeast   => { call => sub { face(Midnight::Map::Direction::SOUTHEAST) },
                     look => 1 },
    south       => { call => sub { face(Midnight::Map::Direction::SOUTH) },
                     look => 1 },
    southwest   => { call => sub { face(Midnight::Map::Direction::SOUTHWEST) },
                     look => 1 },
    west        => { call => sub { face(Midnight::Map::Direction::WEST) },
                     look => 1 },
    northwest   => { call => sub { face(Midnight::Map::Direction::NORTHWEST) },
                     look => 1 },
);

my %command_shortcuts = (
    q   => "quit",
    l   => "look",
    x   => "examine",
    n   => "north",
    ne  => "northeast",
    e   => "east",
    se  => "southeast",
    s   => "south",
    sw  => "southwest",
    w   => "west",
    nw  => "northwest",
);

my (%commands, %conflicts);
for my $key (keys %command_defs) {
    my @bits = split '', $key;
    pop @bits;

    my $cmd = "";
    while (my $bit = shift @bits) {
        $cmd .= $bit;
        if (exists $commands{$cmd}) {
            $conflicts{$cmd}->{$key} = 1;
            $conflicts{$cmd}->{$commands{$cmd}} = 1;
        }
        else {
            $commands{$cmd} = $key;
        }
    }
}
delete $commands{$_} for keys %conflicts;
map { $commands{$_} = $_; delete $conflicts{$_} } keys %command_defs;
map { $commands{$_} = $command_shortcuts{$_}; delete $conflicts{$_} } keys %command_shortcuts;

my $cmd_regex_str = "(?:" . join("|", sort keys %commands) . ")";
my $cmd_regex = qr/^$cmd_regex_str$/i;

my $game = Midnight::Game->new;
my $map = $game->get_map;

my $me = $game->LUXOR;

look();

while (! $game->is_game_over) {
    my $cmd;
    while ($cmd = prompt "What now? ", -until => $cmd_regex) {
        if (exists $conflicts{$cmd}) {
            say "'$cmd' is ambiguous. Did you mean: " . join(" ", sort keys %{$conflicts{$cmd}});
        }
        else {
            say "Sorry, I don't understand '$cmd'.";
        }
    }

    my $defs = $command_defs{$commands{$cmd}};

    if (exists $defs->{call}) {
        $defs->{call}->();
    }

    if (exists $defs->{examine}) {
        examine();
    }
    elsif (exists $defs->{look}) {
        look();
    }   
}

sub quit {
    exit 0;
}

sub look {
    my $location = $me->get_location;
    my $direction = $me->get_direction;

    my $looking = $map->get_looking_towards($location, $direction);

    say $me;
    say "He stands at $location looking $direction at $looking.";
    say "He is " . $me->get_condition . ".";
    say "The ice fear is " . $location->describe_ice_fear . ". " .
        $me->get_name . " is " . $me->get_courage . ".";
    say $me->get_time;

#    my @characters = grep { $_ != $me } @{$me->get_location->get_characters};
#    my $count = @characters;
#    if ($count > 0) {
#        say "There ".PL_V("is", $count)." $count ".PL("person", $count)." here: ".
#            join(" ", sort map { $_->get_name } @characters);
#    }
}

sub examine {
    my $location = $me->get_location;

    my $guard = $location->get_guard;
    if($guard) {
        say "$guard of the " . $guard->get_race . " guard the " . $location->get_feature . ".";
    }

    for my $c (@{$location->get_characters}) {
        next if $c == $me;

        my $line = "$c is here.";

        my $warriors = $c->get_warriors;
        my $riders = $c->get_riders;

        if ($warriors->get_how_many > 0 or $riders->get_how_many > 0) {
            $line .= " He commands ";

            if ($warriors->get_how_many > 0 and $riders->get_how_many > 0) {
                $line .= "$warriors and $riders";
            }
            elsif ($warriors->get_how_many > 0) {
                $line .= $warriors;
            }
            elsif ($riders->get_how_many > 0) {
                $line .= $riders;
            }
            
            $line .= " of the " . $c->get_race . ".";
        }

        say $line;
    }
}

sub commands {
    say "Available commands: " . join(" ", sort keys %command_defs);
}

sub forward {
    if ($me->can_walk_forward) {
        $me->walk_forward;
    }
    else {
        say "He can't go that way!";
    }
}

sub turn {
    my ($turn) = @_;

    if ($turn eq "left") {
        $me->set_direction($me->get_direction->turn_left);
    }
    elsif ($turn eq "right") {
        $me->set_direction($me->get_direction->turn_right);
    }
}

sub face {
    my ($direction) = @_;

    $me->set_direction($direction);
}
